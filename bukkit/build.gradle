plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow'
    id 'com.github.gmazzo.buildconfig'
}

def configVersion = '1.0.21'
def chanceConfigVersion = '1.0.25'
def messageVersion = '1.0.2'

buildConfig {
    className("BuildConstants")
    packageName("com.github.joelgodofwar.mmh")

    buildConfigField("String", "CONFIG_VERSION", "\"${configVersion}\"")
    buildConfigField("String", "CHANCE_CONFIG_VERSION", "\"${chanceConfigVersion}\"")
    buildConfigField("String", "MESSAGE_VERSION", "\"${messageVersion}\"")
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.19.4-R0.1-SNAPSHOT'
    compileOnly 'org.bstats:bstats-bukkit:3.0.2'
    compileOnly 'me.clip:placeholderapi:2.11.5'
    compileOnly 'com.github.EmotionalLove:NmsAdapter:-SNAPSHOT'
    compileOnly 'de.tr7zw:item-nbt-api-plugin:2.11.1'

    // Minecraft 1.18 or less
    compileOnly 'com.mojang:authlib:2.1.28'

    for (Project proj : project(':lib').subprojects) {
        implementation proj
    }

    implementation "commons-io:commons-io:2.15.0"
    implementation "commons-lang:commons-lang:2.6"
    implementation 'org.jetbrains:annotations:19.0.0'
}
shadowJar {
    archiveBaseName.set(tasks.jar.archiveBaseName)
    classifier('')
}
tasks.build.dependsOn(shadowJar)

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        expand(
                'version': version,
                'config_version': configVersion,
                'chance_config_version': chanceConfigVersion,
                'message_version': messageVersion
        )
        include 'plugin.yml'
        include 'config.yml'
        include 'chance_config.yml'
        include 'messages.yml'
    }
}